import Constants
import MirageCompiler/{Token, TokenType}

object Tokenizer

String validNameChars = Constants.alphabet + Constants.numbers + "_"
String validNumberChars = Constants.numbers
String validSymbolChars = "()[]{}<>+-*/=,."

String nameStarters = Constants.alphabet + "#$"
String numberStarters = Constants.numbers
String symbolStarters = validSymbolChars

FSM[TokenState, (String-1, TokenizerState), (Seq[Token], TokenizerState)] symbolFSM = FSM()
{
    // Exclusivity isn't perfectly checked because that's likely impossible. It's checked by verifying that mutual
    // scopes don't have any overlap in their type annotations. This is equivalent to smart dead code detection.
    // For instance char == "." will change String-1 char's type to String-1-Dot and validNumberChars.contains(char)
    // will change String-1 char's type to String-1-In#validNumberChars which has overlap because "." is also in
    // validNumberChars. Additionally String-1-Dot char would cause dead code detection on
    // if (not validNumberChars.contains(char)) { ... }.
    // An ephemeral state is an exclusive state that must transition. This is also compile time checked.
    addExclusiveState(Comment, commentAction)
    addEphemeralState(Escaped, addCharAction)
    addExclusiveState(Float, addCharAction)
    addExclusiveState(Int, addCharAction)
    addEphemeralState(IntDot, addCharAction)
    addExclusiveState(IntDotError, intDotErrorAction)
    addExclusiveState(Name, addCharAction)
    addExclusiveState(NameError, nameErrorAction)
    addExclusiveState(NumberError, numberErrorAction)
    addExclusiveState(String, addCharAction)
    addExclusiveState(Symbol, symbolAction)
    addExclusiveState(SymbolError, symbolErrorAction)
    addExclusiveState(Whitespace, whitespaceAction)

    addTransition(Comment, Whitespace, commentToWhitespace)

    addElseTransition(Escaped, String)

    addTransition(Float, Whitespace, toWhitespace)
    addTransition(Float, Symbol, toSymbol)
    addTransition(Float, Float, numberToNumber)
    addElseTransition(Float, NumberError)

    addTransition(Int, Whitespace, toWhitespace)
    addTransition(Int, Symbol, intToSymbol)
    addTransition(Int, Int, numberToNumber)
    addTransition(Int, IntDot, intToIntDot)
    addElseTransition(Int, NumberError)

    addTransition(IntDot, Float, intDotToFloat)
    addTransition(IntDot, Name, intDotToName)
    addElseTransition(IntDot, IntDotError)

    addTransition(Name, Whitespace, toWhitespace)
    addTransition(Name, Symbol, toSymbol)
    addTransition(Name, Name, nameToName)
    addElseTransition(Name, NameError)

    addTransition(String, Whitespace, stringToWhitespace)
    addTransition(String, Symbol, toSymbol)
    addTransition(String, Escaped, stringToEscaped)

    addTransition(Symbol, Comment, symbolToComment)
    addTransition(Symbol, Int, toInt)
    addTransition(Symbol, Name, toName)
    addTransition(Symbol, String, toString)
    addTransition(Symbol, Symbol, symbolToSymbol)
    addTransition(Symbol, Whitespace, toWhitespace)
    addElseTransition(Symbol, SymbolError)

    addTransition(Whitespace, Int, toInt)
    addTransition(Whitespace, Name, toName)
    addTransition(Whitespace, String, toString)
    addTransition(Whitespace, Symbol, toSymbol)
}

Bool toInt((String-1 char, TokenizerState state))
{
    numberStarters.contains(char)
}

Bool toName((String-1 char, TokenizerState state))
{
    nameStarters.contains(char)
}

Bool toString((String-1 char, TokenizerState state))
{
    char == "\""
}

Bool numberToNumber((String-1 char, TokenizerState state))
{
    validNumberChars.contains(char)
}

Bool nameToName((String-1 char, TokenizerState state))
{
    validNameChars.contains(char)
}

Bool symbolToSymbol((String-1 char, TokenizerState state))
{
    validSymbolChars.contains(char)
}

Bool toWhitespace((String-1 char, TokenizerState state))
{
    char == "\n" or char == " "
}

Bool commentToWhitespace((String-1 char, TokenizerState state))
{
    char == "\n"
}

Bool stringToWhitespace((String-1 char, TokenizerState state))
{
    switch (char)
    case ("\"")
    {
        true
    }
    case ("\n")
    {
        throw TokenizerException(StringNotTerminated, state.lineNum, state.columnNum, "\\n")
    }
    case
    {
        false
    }
}

Bool intToIntDot((String-1 char, TokenizerState state))
{
    char == "."
}

Bool intDotToFloat((String-1 char, TokenizerState state))
{
    Constants.numbers.contains(char)
}

Bool intDotToName((String-1 char, TokenizerState state))
{
    nameStarters.contains(char)
}

Bool symbolToComment((String-1 char, TokenizerState state))
{
    char == "/" and state.symbolText == "/"
}

Bool toSymbol((String-1 char, TokenizerState state))
{
    symbolStarters.contains(char)
}

Bool intToSymbol((String-1 char, TokenizerState state))
{
    char != "." and symbolStarters.contains(char)
}

Bool stringToEscaped((String-1 char, TokenizerState state))
{
    char == "\\"
}

(Seq[Token], TokenizerState) addCharAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    TokenizerState newState = copy (tokenizerState)
    {
        symbolText += char
        columnNum += 1
    }

    (Nil, newState)
}

(Seq[Token], TokenizerState) intDotErrorAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    throw TokenizerException(UnexpectedDot, state.lineNum, state.columnNum, char)
}

(Seq[Token], TokenizerState) numberErrorAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    throw TokenizerException(BadCharInNumber, state.lineNum, state.columnNum, char)
}

(Seq[Token], TokenizerState) nameErrorAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    throw TokenizerException(BadCharInName, state.lineNum, state.columnNum, char)
}

(Seq[Token], TokenizerState) symbolErrorAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    throw TokenizerException(BadCharInSymbol, state.lineNum, state.columnNum, char)
}

(Seq[Token], TokenizerState) commentAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    (Nil, tokenizerState)
}

Seq[Token] parseSymbols(TokenizerState tokenizerState)
{
    Seq[String] splits = tokenizerState.symbolText
        .split((String text)
        {
            Int validIndexFromEnd = 0.until(text.size)
                .find((Int i)
                {
                    TokenType.dictionary.get(text.split(0, text.size - i))
                })
                .getOrElse(throw TokenizerException(
                    UnknownSymbol,
                    tokenizerState.lineNum
                    tokenizerState.symbolStartColumn,
                    tokenizerState.symbolText))

            text.size - validIndexFromEnd
        })

    (Seq[Token] tokens, Int charCount) = splits.fold((Nil, 0), ((Seq[Token] tokens, Int charCount), String split)
        {
            Token newToken = Token(
                TokenType.dictionary.get(split),
                split
                tokenizerState.lineNum,
                tokenizerState.symbolStartColumn + charCount)

            (tokens + newToken, charCount + split.size)
        })

    tokens

(Seq[Token], TokenizerState) unpackTokens(TokenizerState tokenizerState, TokenState previousTokenState)
{
    if (tokenizerState.symbolText.size > 0)
    {
        Seq[Token] tokens = switch (previousTokenState)
        case (TokenState.Float)
        {
            Seq(Token.apply(Float, tokenizerState))
        }
        case (TokenState.Int)
        {
            Seq(Token.apply(Int, tokenizerState))
        }
        case (TokenState.Name)
        {
            // Check if this is a keyword and use the appropriate token type
            Seq(Token.apply(TokenType.dictionary.get(tokenizerState.symbolText).getOrElse(Name), tokenizerState))
        }
        case (TokenState.String)
        {
            Seq(Token.apply(String, tokenizerState))
        }
        case (TokenState.Symbol)
        {
            parseSymbols(tokenizerState)
        }
        case
        {
            Nil
        }

        TokenizerState newState = copy (tokenizerState)
        {
            symbolText = ""
            symbolStartColumn = columnNum
        }

        (tokens, newState)
    }
    else
    {
        (Nil, tokenizerState)
    }
}


(Seq[Token], TokenizerState) symbolAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    (Seq[Token] tokens, TokenizerState unpackedState) = if (previousTokenState == TokenState.Symbol)
    {
        (Nil, tokenizerState)
    }
    else
    {
        unpackTokens(tokenizerState, previousTokenState)
    }

    TokenizerState newState = copy (unpackedState)
    {
        symbolText += char
        columnNum += 1
    }

    (tokens, newState)
}

(Seq[Token], TokenizerState) whitespaceAction(
    TokenState previousTokenState,
    (String-1 char, TokenizerState tokenizerState))
{
    (Seq[Token] previousTokens, TokenizerState unpackedState) = unpackTokens(tokenizerState, previousTokenState)
    (Option[Token] maybeToken, TokenizerState newTokenizerState) = if (char == "\n")
    {
        Token newToken = Some(Token(Newline, "\\n", unpackedState.lineNum, unpackedState.columnNum))
        TokenizerState newState = copy (unpackedState)
        {
            lineNum += 1
            symbolStartColumn = 1
            columnNum = 1
        }
        (Some(newToken), newState)
    }
    else
    {
        TokenizerState newState = copy (unpackedState)
        {
            symbolStartColumn += 1
            columnNum += 1
        }
        (None, newState)
    }

    (previousTokens + maybeToken, newTokenizerState)
}

(Seq[Token], TokenizerState) step(String-1 char, (Seq[Token] tokens, TokenizerState state))
{
    (Seq[Token] newTokens, TokenizerState newState) = FSM.step((char, state))

    (tokens + newTokens, newState)
}

public Seq[Token] tokenize(String plaintext) throws TokenizerException
{
    (Seq[Token] tokens, TokenizerState state) = (plaintext + "\n").fold((Nil, TokenizerState("", 1, 1, 1)), step)
    tokens
}
